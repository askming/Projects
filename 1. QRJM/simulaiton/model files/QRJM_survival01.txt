model{
	zero[1] <- 0
	zero[2] <- 0

	for (i in 1:I){
		# prior for random effects
		u[i, 1:2] ~ dmnorm(zero[], precision[,])

		# survival process, baseline hazard is set to 1
		A[i] <- alpha2*u[i,2] + alpha1*delta[2]*H[i,2]
		B[i] <- alpha1*delta[1]*H[i,1] + alpha2*u[i,1] + inprod(gamma, W[i,])
		S[i] <- exp(- exp(B[i])*(pow(exp(A[i]), Ti[i])-1)/A[i])
		h[i] <- c*exp(inprod(gamma, W[i,]) + alpha1*(delta[1]*H[i,1] + delta[2]*H[i,2]*Ti[i]) + alpha2*(u[i,1] + u[i,2]*Ti[i]))
		L[i] <- pow(h[i], event[i])*S[i]/1.0E+08

		# zero trick
		phi[i] <- -log(L[i])
		zeros[i] ~ dpois(phi[i])

	}#end of i loop

	precision[1:2,1:2] <- inverse(Sigma[,])
  	Sigma[1,1] <- pow(w11, 2)
  	Sigma[1,2] <- w21*w11
  	Sigma[2,1] <- Sigma[1,2]
  	Sigma[2,2] <- pow(w22,2) + pow(w21,2)
  	rho <- Sigma[2,1]/sqrt(Sigma[1,1]*Sigma[2,2])


	# priors for other parameters
	delta[1] ~ dnorm(0, 0.001)
	delta[2] ~ dnorm(0, 0.001)
	gamma[1] ~ dnorm(0, 0.001)
	gamma[2] ~ dnorm(0, 0.001)
	alpha1 ~ dnorm(0, 0.001)
	alpha2 ~ dnorm(0, 0.001)
	w11 ~ dunif(0.1, 10)
	w22 ~ dunif(0.1, 10)
	w21 ~ dnorm(0, 0.1)
	c ~ dunif(0.01, 10)
}