model{
	zero[1] <- 0
	zero[2] <- 0
	k1 <- (1-2*qt)/(qt*(1-qt))
	k2 <- 2/(qt*(1-qt))

	for (i in 1:I){
	# prior for random effects
	u[i, 1:2] ~ dmnorm(zero[], precision[,])

	# longitudinal process, BQR mixed model using ALD representation
		for (j in 1:J[i]){
			er[i,j] ~ dexp(sigma)
			mu[i,j] <- u[i,1] + u[i,2]*t[j] + inprod(X[i,], beta[]) + delta[1]*H[i,1] + delta[2]*H[i,2]*t[j] + k1*er[i,j]
			prec[i,j] <- sigma/(k2*er[i,j])
			y[i,j] ~ dnorm(mu[i,j], prec[i,j])
		} #end of j loop		

	# survival process, baseline hazard is set to 1
	A[i] <- alpha2*u[i,2] + alpha1*delta[2]*H[i,2]
	B[i] <- alpha1*delta[1]*H[i,1] + alpha2*u[i,1] + inprod(gamma, W[i,])
	S[i] <- exp(- exp(B[i])*(pow(exp(A[i]), Ti[i])-1)/A[i])
	h[i] <- exp(inprod(gamma, W[i,]) + alpha1*(delta[1]*H[i,1] + delta[2]*H[i,2]*Ti[i]) + alpha2*(u[i,1] + u[i,2]*Ti[i]))
	L[i] <- pow(h[i], event[i])*S[i]/1.0E+08

	# zero trick
	phi[i] <- -log(L[i])
	zeros[i] ~ dpois(phi[i])

				#z[i,1] ~ dnorm(0, 1)
				#z[i,2] ~ dnorm(0, 1)
				#u[i,1] <- z[i,1]
				#u[i,2] <- w21*z[i,1] + w22*z[i,2]

	}#end of i loop

	precision[1:2,1:2] <- inverse(Sigma[,])
  	Sigma[1,1] <- 1
  	Sigma[1,2] <- rho*sig1
  	Sigma[2,1] <- Sigma[1,2]
  	Sigma[2,2] <- sig1*sig1

				#w22 ~ dunif(0, 10)
				#w21 ~ dnorm(0, 0.01)
				#Sigma[1,1] <- 1
				#Sigma[1,2] <- w21
				#Sigma[2,1] <- w21
				#Sigma[2,2] <- w21*w21 + w22*w22

	# priors for other parameters
	alpha1 ~ dnorm(0, 0.001)
	alpha2 ~ dnorm(0, 0.001)
	beta[1] ~ dnorm(0, 0.001)
	beta[2] ~ dnorm(0, 0.001)
	delta[1] ~ dnorm(0, 0.001)
	delta[2] ~ dnorm(0, 0.001)
	gamma[1] ~ dnorm(0, 0.001)
	gamma[2] ~ dnorm(0, 0.001)
	sigma ~ dgamma(0.001, 0.001)
	rho ~ dunif(-1, 1)
	sig1 ~  dgamma(0.01, 0.01)
}